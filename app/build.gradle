plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.appdistribution'
}

android {
    namespace 'com.jvrcoding.weatherapp'
    compileSdk 33

    defaultConfig {
        applicationId "com.jvrcoding.weatherapp"
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        def apiKey = System.getenv("API_KEY")

        if (apiKey == null) {
            Properties properties = new Properties()
            properties.load(project.rootProject.file("local.properties").newDataInputStream())
            apiKey = properties.getProperty("api_key")
        }

        buildConfigField "String", "API_KEY", "\"${apiKey}\""
    }

    buildTypes {
        debug {
            firebaseAppDistribution {
                artifactType="APK"
                groups="sample-group"
                releaseNotesFile="$rootDir/releasenotes.txt"
                serviceCredentialsFile = System.getenv("FIREBASE_SERVICE_CREDENTIALS") != null
                        ? project.file("$System.env.GITHUB_WORKSPACE/firebase-service-account.json")
                        : project.file("$rootDir/weatherapp-e6456-93d7def3f886.json")
            }
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            firebaseAppDistribution {
                artifactType="APK"
                groups="sample-group"
                releaseNotesFile="$rootDir/releasenotes.txt"
                serviceCredentialsFile="$rootDir/weatherapp-e6456-93d7def3f886.json"
            }
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_ui_version
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"

    //compose
    implementation 'androidx.activity:activity-compose:1.7.1'
    implementation "androidx.compose.ui:ui:$compose_ui_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    implementation "androidx.compose.material:material:$compose_ui_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_ui_version"
    implementation "androidx.navigation:navigation-compose:2.6.0-rc01"
    implementation "androidx.hilt:hilt-navigation-compose:1.1.0-alpha01"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"

    implementation 'com.google.accompanist:accompanist-swiperefresh:0.20.0'

    //dagger
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    kapt "androidx.hilt:hilt-compiler:1.0.0"

    //retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'

    implementation 'com.google.code.gson:gson:2.9.0'
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"

    // Room
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    //bypass time
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'

    //image loading
    implementation "io.coil-kt:coil-compose:2.2.2"

    //datastore
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    // Local unit tests
    testImplementation "androidx.test:core:1.5.0"
    testImplementation "junit:junit:4.13.2"
    testImplementation "androidx.arch.core:core-testing:2.2.0"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4"
    testImplementation "com.google.truth:truth:1.1.3"
    testImplementation "io.mockk:mockk:1.10.5"
}